import java.util.regex.Pattern

// This simple grabs the latest 'forge' plugin, or ForgeGradle, from Forge's servers.
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

// We apply the 'forge' plugin, so ForgeGradle can do it's stuff.
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

group = 'powercrystals' // Your main package.
archivesBaseName = 'MineFactoryReloaded'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.mod_version = pullVersion()

repositories {
    maven {
        name = "ChickenBones repo"
        url = "http://chickenbones.net/maven/"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net/"
    }
    maven {
        name = "Player's Maven"
        url = "http://maven.ic2.player.to/"
    }
    maven { url "http://dvs1.progwml6.com/files/maven" }
    maven {
        name "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
}

dependencies {
    compile "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:deobf"
    compile "cofh:CoFHCore:${mc_version}-${cofhcore_version}:deobf"
    compile "cofh:RedstoneFlux:${rf_mc_version}-${rf_version}:deobf"
    compile "cofh:CoFHWorld:${mc_version}-${cofhworld_version}:deobf"
    compile "net.industrial-craft:industrialcraft-2:${ic2_version}"
    compile "net.sengir.forestry:forestry_${mc_version}:${forestry_version}"
    //deobfCompile "com.mod-buildcraft:buildcraft-api:${buildcraft_version}"
    //deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"
}
version = "${mod_version}." + (System.getenv("BUILD_NUMBER") ?: "1")

sourceSets {
    api {
        java {
            srcDir 'src/api/java'
        }
    }
    main_api {
        java {
            srcDir 'src/main/java/powercrystals/minefactoryreloaded/api'
        }
        compileClasspath += sourceSets.api.compileClasspath
    }
    core {
        java {
            srcDir 'src/core/java'
        }
        resources {
            srcDir 'src/core/resources'
        }
        compileClasspath += sourceSets.api.compileClasspath
    }
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
        compileClasspath += sourceSets.api.output
        compileClasspath += sourceSets.core.output
    }
}

minecraft {
    version = mc_version + "-" + forge_version
    mappings = mcp_mappings
    runDir = "run"
    replace '${mod_version}', mod_version
    makeObfSourceJar = false
}

task coreJar(type: Jar) {
    from sourceSets.core.output
    appendix = 'core'
    version = "${mfr_core_version}"
}

processResources {
    dependsOn 'coreJar'
    inputs.property "version", version
    inputs.property "mc_version", mc_version

    filesMatching('mcmod.info') {
        expand 'version': project.mod_version, 'mc_version': mc_version
    }
    filesMatching('META-INF/MANIFEST.MF') {
            expand 'core_version': project.mfr_core_version, 'version': project.mod_version
    }
    rename '(.+)\\{core_version\\}.meta', '$1' + "${mfr_core_version}" + '.meta'
    from(coreJar.archivePath) {
        into 'META-INF/libraries'
    }
}

version = "${project.minecraft.version}-${project.version}"

jar {
    classifier = 'universal'
    manifest { from sourceSets.main.output.resourcesDir.toString() + '/META-INF/MANIFEST.MF' }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest = jar.manifest
}

task srcJar(type: Jar) {
    from sourceSets.main_api.allSource
    classifier = 'api'
}

// Tell the artifact system about our extra jars
artifacts {
    archives deobfJar
    archives srcJar
}

def pullVersion() {
    def file = file("src/main/java/powercrystals/minefactoryreloaded/MFRProps.java")
    def version = ""
    def pattern = Pattern.compile("VERSION = \"([^\"]*)\";")
    file.eachLine { String s ->
        def matcher = pattern.matcher(s)
        if (matcher.find()) {
            version = matcher.group(1)
            return
        }
    }
    return version
}

// Configure an upload task.
uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("local_maven")) {
                repository(url: "file://${local_maven}")
                pom {
                    groupId = project.group
                    // Force the maven upload to use the <mcversion>-<version> syntax preferred at files
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'MinefactoryReloaded'
                        url 'https://github.com/skyboy/MinefactoryReloaded'

                        scm {
                            url 'https://github.com/skyboy/MinefactoryReloaded'
                            connection 'scm:git:git://github.com/skyboy/MinefactoryReloaded.git'
                            developerConnection 'scm:git:git@github.com:skyboy/MinefactoryReloaded.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/skyboy/MinefactoryReloaded/issues'
                        }
                    }
                }
            }
        }
	}
}
